// 干.mustRunAfter('base/maven')
// 干.mustRunAfter('base/changelog')

if (project.findProperty('enable_publishing') != 'true') {
	return
}

def bintray_user = 干.proj('bintray_user', 'username for bintray (or unset enable_publishing)')
def bintray_pass = 干.proj('bintray_pass', 'password for bintray (or unset enable_publishing)')
def nexus_user =   干.proj('nexus_user', 'username for nexus/mavencentral (or unset enable_publishing)')
def nexus_pass =   干.proj('nexus_pass', 'password for nexus/mavencentral (or unset enable_publishing)')

// subset of base/maven
def maven_artifact  = project.name
def license         = 干.proj('license', 'supported: apache')
def license_bintray = 干.prop('base/maven', "${license}_bintray")
def git_url         = 干.proj('git_url', 'the git url with no protocol, e.g.: github.com/diffplug/blowdryer')

// upload releases to bintray and then mavenCentral
apply plugin: 'com.jfrog.bintray'
bintray {
	user = bintray_user
	key =  bintray_pass
	publications = project.publishing.publications.names
	publish = true
	pkg {
		repo = 'opensource'
		name = maven_artifact
		userOrg = 'diffplug'
		licenses = [license_bintray]
		vcsUrl = "https://${git_url}.git"
		version {
			name = project.version
			mavenCentralSync {
				user =     nexus_user
				password = nexus_pass
			}
		}
	}
}
