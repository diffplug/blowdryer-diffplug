if (tasks.names.contains('changelogCheck')) {
	// set the project version for POM, jar manifest, etc.
	version = spotlessChangelog.versionNext
	// ensures that nothing will be built if changelogPush will end up failing
	tasks.named('jar').configure {
		dependsOn tasks.named('changelogCheck')
	}
	// ensures that changelog bump and push only happens if the publish was successful
	tasks.named('changelogBump').configure {
		dependsOn tasks.named('bintrayUpload')
		// only the root project has bintrayUpload, and it finalizes all bintrayUpload tasks
		// https://github.com/bintray/gradle-bintray-plugin/blob/3fe02dfdae3e807afba57e0140a0d4c2424674e1/src/main/groovy/com/jfrog/bintray/gradle/ProjectsEvaluatedBuildListener.groovy#L37-L41
		dependsOn rootProject.tasks.named('bintrayPublish')
	}
} else {
	// same as above, but pull changelog stuff from the parent changelog
	version = parent.spotlessChangelog.versionNext
	tasks.named('jar').configure {
		dependsOn parent.tasks.named('changelogCheck')
	}
	parent.tasks.named('changelogBump').configure {
		dependsOn tasks.named('bintrayUpload')
		dependsOn rootProject.tasks.named('bintrayPublish')
	}
}
